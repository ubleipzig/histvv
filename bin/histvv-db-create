#!/usr/bin/perl

use strict;
use warnings;

use Histvv;
use Histvv::CLI;
use Histvv::Db;
use Histvv::Search;
use File::Basename;
use File::Temp;
use File::Copy;
use File::Spec;

my $cli  = Histvv::CLI->new();
my %opts = $cli->get_opts('db|d=s' => undef, 'force|f' => 0, 'cleanup' => 0);

my $dbfile = $opts{db} || $cli->error("Please specify a database!");

$cli->error( "No input files!" ) unless @ARGV;

if (-f $dbfile) {
    unless ($opts{force}) {
        print "File $dbfile exists. Replace? [Y/n]: ";
        my $y = <STDIN>;
        chomp $y if defined $y;
        exit if $y =~ /^n(o)?$/i;
    }

    # Only want a file name but let File::Temp figure out where to
    # create the temp file. That's why we are not using mktemp here
    # but instead use the OO interface in its own block so that
    # File::Temp cleans up behind itself when we leave scope and we
    # only keep the file name.
    {
        my $tmp = File::Temp->new( SUFFIX => '.dbxml' );
        $dbfile = $tmp->filename;
    }
    $cli->chatter("Creating temporary file $dbfile...");
}

my $db = Histvv::Db->new( $dbfile, { create => 1, private => 1 } );

my $ns_xml = 'http://www.w3.org/XML/1998/namespace';
my $ns_histvv = $Histvv::XMLNS;

$cli->chatter("Adding indexes...");
$db->add_index( $ns_xml,    'id',            'node-attribute-presence' );
$db->add_index( $ns_xml,    'id',            'node-attribute-equality-string' );
$db->add_index( $ns_histvv, 'vv',            'node-element-presence' );
$db->add_index( $ns_histvv, 'dozentenliste', 'node-element-presence' );
$db->add_index( $ns_histvv, 'veranstaltung', 'node-element-presence' );
$db->add_index( $ns_histvv, 'nachname',      'node-element-equality-string' );
$db->add_index( $ns_histvv, 'nachname',      'node-element-substring-string' );
$db->add_index( $ns_histvv, 'nachname',      'node-element-presence' );
$db->add_index( $ns_histvv, 'vorname',       'node-element-equality-string' );
$db->add_index( $ns_histvv, 'vorname',       'node-element-equality-string' );
$db->add_index( '',         'ref',           'node-attribute-equality-string' );
$db->add_index( '',         'x-text',        'node-attribute-substring-string' );
$db->add_index( '',         'x-dozent',      'node-attribute-equality-string' );
$db->add_index( '',         'x-dozent',      'node-attribute-substring-string' );
$db->add_index( '',         'x-semester',    'node-attribute-equality-string' );

$cli->chatter( "Loading files..." );
foreach my $file (@ARGV) {
    $cli->chatter( "  $file ..", 1 );
    my $name = basename($file);
    my $doc = Histvv::Search::annotate_file($file);
    my $xml = $doc->toString(0);
    $db->put_doc($xml, $name);
    $cli->chatter( " ok" );
}

# sync database status to disc
$cli->chatter( 'Syncing database...' );
$db->{container}->sync();

# FIXME: there should be a better way to cleanly disconnect from the
# database, which we need to do before we copy/move the db file
undef $db;

if ( $dbfile ne $opts{db} ) {
    $cli->chatter("Copying temporary $dbfile -> $opts{db}...");
    File::Copy::copy( $dbfile, $opts{db} ) or die $!;
    unlink $dbfile;

    if ( $opts{cleanup} ) {
        require File::Find;
        my @files;
        my $dir = dirname $opts{db};
        File::Find::find( sub { push( @files, $_ ) if m/^__db\.[0-9]{3}$/ },
            $dir );
        for (@files) {
            my $f = File::Spec->catfile( $dir, $_ );
            $cli->chatter("removing $f");
            unlink $f;
        }
    }
}

__END__

=head1 NAME

histvv-db-create - create a new Berkeley DB XML database

=head1 SYNOPSIS

  histvv-db-create [-v] [-f] -d dbfile file1.xml ...
  histvv-db-create --help | --man

=head1 DESCRIPTION

I<histvv-db-create> creates a new Berkeley DB XML database and loads
the passed files into it. If the file specified with the B<--db>
option already exists it will ask whether the existing file should be
replaced by the new database.

=head1 OPTIONS

=over

=item B<--db>, B<-d>

The file path where to create the new database.

=item B<--force>, B<-f>

Replace an existing file without prompting the user.

=item B<--cleanup>

Remove stale region files when replacing a database. Region files are
named C<__db.001>, C<__db.002>, and so on and are created by the DB
environment to back shared memory regions.

=item B<--verbose>, B<-v>

Display progress messages.

=item B<--help>, B<-h>

Display short help message and exit. If used together with B<-v> the
entire manpage will be displayed.

=item B<--man>

Display manpage and exit. This is equivalent to B<-h> B<-v>.

=back

=head1 SEE ALSO

L<Histvv::Db>

=head1 AUTHOR

Carsten Milling, C<< <cmil at hashtable.de> >>

=head1 COPYRIGHT & LICENSE

Copyright 2008 Carsten Milling, all rights reserved.

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl itself.

=cut
